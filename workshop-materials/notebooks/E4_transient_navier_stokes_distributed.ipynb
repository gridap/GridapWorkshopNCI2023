{
 "cells": [
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Gridap, GridapDistributed, GridapGmsh, PartitionedArrays\n",
    "using DrWatson\n",
    "\n",
    "np = 2\n",
    "ranks = with_debug() do distribute\n",
    "  distribute(LinearIndices((np,)))\n",
    "end\n",
    "\n",
    "msh_file = projectdir(\"meshes/perforated_plate_tiny.msh\")\n",
    "model = GmshDiscreteModel(ranks,msh_file)\n",
    "\n",
    "k = 2\n",
    "reffeᵤ = ReferenceFE(lagrangian,VectorValue{2,Float64},k)\n",
    "reffeₚ = ReferenceFE(lagrangian,Float64,k-1)\n",
    "\n",
    "V = TestFESpace(model,reffeᵤ,conformity=:H1,dirichlet_tags=[\"inlet\",\"walls\",\"cylinder\"])\n",
    "Q = TestFESpace(model,reffeₚ,conformity=:C0)\n",
    "\n",
    "const Tth = 2\n",
    "const Uₘ = 1.5\n",
    "const H  = 0.41\n",
    "ξ(t) = (t <= Tth) ? sin(π*t/(2*Tth)) : 1.0\n",
    "u_in(x,t::Real) = VectorValue( 4 * Uₘ * x[2] * (H-x[2]) / (H^2) * ξ(t), 0.0 )\n",
    "u_w(x,t::Real)  = VectorValue(0.0,0.0)\n",
    "u_c(x,t::Real)  = VectorValue(0.0,0.0)\n",
    "u_in(t::Real)   = x -> u_in(x,t)\n",
    "u_w(t::Real)    = x -> u_w(x,t)\n",
    "u_c(t::Real)    = x -> u_c(x,t)\n",
    "\n",
    "U = TransientTrialFESpace(V,[u_in,u_w,u_c])\n",
    "P = TrialFESpace(Q)\n",
    "\n",
    "Y = MultiFieldFESpace([V, Q])\n",
    "X = TransientMultiFieldFESpace([U, P])\n",
    "\n",
    "degree = 2*k\n",
    "Ω  = Triangulation(model)\n",
    "dΩ = Measure(Ω,degree)\n",
    "\n",
    "Γ_out = BoundaryTriangulation(model,tags=\"outlet\")\n",
    "n_Γout = get_normal_vector(Γ_out)\n",
    "dΓ_out = Measure(Γ_out,degree)\n",
    "\n",
    "const Re = 100.0\n",
    "conv(u,∇u) = Re*(∇u')⋅u\n",
    "dconv(du,∇du,u,∇u) = conv(u,∇du)+conv(du,∇u)\n",
    "\n",
    "m((u,p),(v,q)) = ∫( u⋅v )dΩ\n",
    "a((u,p),(v,q)) = ∫( ∇(v)⊙∇(u) - (∇⋅v)*p + q*(∇⋅u) )dΩ\n",
    "c(u,v) = ∫( v⊙(conv∘(u,∇(u))) )dΩ\n",
    "dc(u,du,v) = ∫( v⊙(conv∘(u,∇(du)) + conv∘(du,∇(u))) )dΩ\n",
    "\n",
    "res(t,(u,p),(v,q)) = m((∂t(u),p),(v,q)) + a((u,p),(v,q)) + c(u,v)\n",
    "jac(t,(u,p),(du,dp),(v,q)) = a((du,dp),(v,q)) + dc(u,du,v)\n",
    "jac_t(t,(u,p),(dut,dpt),(v,q)) = m((dut,dpt),(v,q))\n",
    "op = TransientFEOperator(res,jac,jac_t,X,Y)\n",
    "\n",
    "nls = Gridap.Algebra.NewtonRaphsonSolver(LUSolver(),1.e-6,10)\n",
    "Δt  = 0.01\n",
    "θ   = 0.5\n",
    "ode_solver = ThetaMethod(nls,Δt,θ)\n",
    "\n",
    "x₀ = interpolate_everywhere([VectorValue(0.0,0.0),0.0],X(0.0))\n",
    "t₀ = 0.0\n",
    "T  = Tth\n",
    "xₕₜ = solve(ode_solver,op,x₀,t₀,T)\n",
    "\n",
    "dir = datadir(\"ins_distributed\")\n",
    "i_am_main(ranks) && !isdir(dir) && mkdir(dir)\n",
    "for (xₕ,t) in xₕₜ\n",
    "  println(\" > Computing solution at time $t\")\n",
    "  uₕ,pₕ = xₕ\n",
    "  file = dir*\"/solution_$t\"*\".vtu\"\n",
    "  writevtk(Ω,file,cellfields=[\"u\"=>uₕ,\"p\"=>pₕ])\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.2"
  },
  "kernelspec": {
   "name": "julia-1.9",
   "display_name": "Julia 1.9.2",
   "language": "julia"
  }
 },
 "nbformat": 4
}
